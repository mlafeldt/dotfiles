""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Most of this is based on Gary Bernhardt's .vimrc file:
" https://github.com/garybernhardt/dotfiles/blob/master/.vimrc
" vim: set ts=2 sts=2 sw=2 expandtab:
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" BASIC EDITING CONFIGURATION
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" enter vim mode
set nocompatible

" set up Vundle
filetype off
set rtp+=~/.vim/bundle/Vundle.vim
call vundle#begin()

" list of plugins installed by Vundle
Plugin 'gmarik/Vundle.vim'
Plugin 'altercation/vim-colors-solarized'
Plugin 'cespare/vim-toml'
Plugin 'ctrlpvim/ctrlp.vim'
Plugin 'ervandew/supertab'
Plugin 'fatih/vim-go'
Plugin 'fatih/vim-hclfmt'
Plugin 'godlygeek/tabular'
Plugin 'hashivim/vim-terraform'
Plugin 'Jimdo/vim-spec-runner'
Plugin 'jparise/vim-graphql'
Plugin 'Matt-Deacalion/vim-systemd-syntax'
Plugin 'mileszs/ack.vim'
Plugin 'moby/moby', {'rtp': '/contrib/syntax/vim/'}
Plugin 'preservim/nerdtree'
Plugin 'rust-lang/rust.vim'
Plugin 'tpope/vim-commentary'
Plugin 'tpope/vim-fugitive'
Plugin 'tpope/vim-markdown'
Plugin 'tpope/vim-rhubarb'
Plugin 'unkiwii/vim-nerdtree-sync'

" end of Vundle setup
call vundle#end()
filetype plugin indent on

set encoding=utf-8
" allow unsaved background buffers and remember marks/undo for them
set hidden
" remember more commands and searches
set history=10000
" always show status line
set laststatus=2
" custom status line
set statusline=%<%f\ (%{&ft})\ %-4(%m%)%=%-19(%3l,%02c%03V%)
" highlight current line
set cursorline
" height of command line
set cmdheight=2
" jump to the first open window that contains the specified buffer
set switchbuf=useopen
" show line numbers
set number
" minimal number of columns to use for the line number
set numberwidth=5
" always show tab page labels
set showtabline=2
" minimal number of columns for current window
set winwidth=79
" prevent vim from clobbering the scrollback buffer
set t_ti= t_te=
" keep more context when scrolling off the end of a buffer
set scrolloff=5
" store temporary files in a central spot
set backup
set backupdir=~/.vim-tmp//,~/.tmp//,~/tmp//,/var/tmp//,/tmp//
set directory=~/.vim-tmp//,~/.tmp//,~/tmp//,/var/tmp//,/tmp//
" allow backspacing over everything in insert mode
set backspace=indent,eol,start
" display incomplete commands
set showcmd
" enable syntax highlighting
syntax on
" use emacs-style tab completion when selecting files etc.
set wildmode=longest,list
" make tab completion for files/buffers act like bash
set wildmenu
" show line and column number of the cursor position
set ruler
" highlight screen column
set colorcolumn=81
" better list strings
set listchars=tab:▸\ ,eol:¬,extends:❯,precedes:❮
" yank and paste with the system clipboard
set clipboard=unnamed
" tweak keyword detection for search & completion
let g:sh_noisk=1
set iskeyword+=-
" Insert only one space when joining lines that contain sentence-terminating
" punctuation like `.`.
set nojoinspaces
" do not wrap lines
set nowrap
" auto-reload files on changes
set autoread

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" COLORS
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

set background=dark
let g:solarized_termtrans = 1
colorscheme solarized

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" MAPPINGS
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" set leader key to comma
let mapleader=","

" open files in directory of current file
cnoremap %% <C-R>=expand('%:h').'/'<cr>
nmap <leader>e :edit %%
nmap <leader>v :view %%

" toggle files
nnoremap <leader><leader> <c-^>

" toggle folds
nnoremap <space> za

" spell-check current file
nnoremap <leader>S :!aspell --check --dont-backup '%'<cr>

" re-hardwrap paragraphs of text
nnoremap <leader>q gwip

" create markdown h1 and h2 headings
nnoremap <leader>1 yypVr=
nnoremap <leader>2 yypVr-

" open current file with Marked app
nnoremap <leader>m :!marked '%'<cr><cr>

" save file with sudo
cmap w!! '%'!sudo tee > /dev/null '%'

" rename current file
function! RenameFile()
  let old_name = expand('%')
  let new_name = input('New file name: ', expand('%'), 'file')
  if new_name != '' && new_name != old_name
    exec ':saveas ' . new_name
    exec ':silent !rm ' . old_name
    redraw!
  endif
endfunction
nnoremap <leader>n :call RenameFile()<cr>

" promote variable to rspec let
function! PromoteToLet()
  normal! dd
  normal! P
  .s/\(\w\+\) = \(.*\)$/let(:\1) { \2 }/
  normal ==
endfunction
command! PromoteToLet :call PromoteToLet()
nnoremap <leader>p :PromoteToLet<cr>

" insert hash rocket with ctrl+l
imap <c-l> <space>=><space>

" disable arrow keys
"map <up> <nop>
"map <down> <nop>
"map <left> <nop>
"map <right> <nop>

" move by screen line
nnoremap j gj
nnoremap k gk

" open new vertical/horizontal split and switch over to it
nnoremap <leader>w <c-w>v<c-w>l
nnoremap <leader>W <c-w>s<c-w>j

" move around splits more easily
nnoremap <c-h> <c-w>h
nnoremap <c-j> <c-w>j
nnoremap <c-k> <c-w>k
nnoremap <c-l> <c-w>l

" remove smart quotes, etc.
function! RemoveFancyCharacters()
    let typo = {}
    let typo["“"] = '"'
    let typo["”"] = '"'
    let typo["‘"] = "'"
    let typo["’"] = "'"
    let typo["–"] = '--'
    let typo["—"] = '---'
    let typo["…"] = '...'
    :exe ":%s/".join(keys(typo), '\|').'/\=typo[submatch(0)]/ge'
endfunction
command! RemoveFancyCharacters :call RemoveFancyCharacters()

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" SEARCHING & MOVING
" http://stevelosh.com/blog/2010/09/coming-home-to-vim/
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

set ignorecase
set smartcase
set incsearch
set showmatch
set hlsearch
nnoremap <leader><space> :nohlsearch<cr>
nnoremap <tab> %
vnoremap <tab> %

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" CTRLP PLUGIN
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

let g:ctrlp_map = '<leader>f'
nmap <leader>F :CtrlP %%<cr>
nmap <leader>l :CtrlPBuffer<cr>

let g:ctrlp_match_window = 'bottom,order:ttb,min:1,max:20'
let g:ctrlp_use_caching = 0

set wildignore+=*/vendor/*,*/Godeps/_workspace/*,*/target/*,*/tmp/*,*/node_modules/*,*.class,*/cdk.out/*

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" FUGITIVE PLUGIN AND OTHER GIT STUFF
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

nnoremap <leader>d :!git diff '%'<cr>
nnoremap <leader>D :!git diff<cr>

" open a split for each dirty file in git
function! OpenChangedFiles()
  only " Close all windows, unless they're modified
  let status = system('git status -s | grep "^ \?\(M\|A\|UU\)" | sed "s/^.\{3\}//"')
  let filenames = split(status, "\n")
  exec "edit " . filenames[0]
  for filename in filenames[1:]
    exec "vsp " . filename
  endfor
endfunction
command! OpenChangedFiles :call OpenChangedFiles()

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" SUPERTAB
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

let g:SuperTabDefaultCompletionType = "context"
let g:SuperTabContextTextOmniPrecedence = ['&omnifunc', '&completefunc']

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" VIM-GO
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

let g:go_fmt_command = "gofumpt"
let g:go_gopls_complete_unimported = 1
let g:go_code_completion_icase = 1
let g:go_fold_enable = []

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" RUST.VIM
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

let g:rustfmt_autosave = 1

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" VIM-COMMENTARY
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

xmap \\  <Plug>Commentary
nmap \\\ <Plug>CommentaryLine
nmap \\u <Plug>CommentaryUndo

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" VIM-TERRAFORM
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

let g:terraform_align = 1
let g:terraform_fold_sections = 0
let g:terraform_fmt_on_save = 1

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" NERDTREE
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

let g:NERDTreeDirArrowExpandable = ""
let g:NERDTreeDirArrowCollapsible = ""

let g:nerdtree_sync_cursorline = 1

nnoremap <leader>N :NERDTreeToggleVCS<cr>

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" CUSTOM AUTOCMDS
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

augroup vimrcEx
  " clear all autocmds in this group
  autocmd!

  " open NERDTree when vim starts up if no files were specified
  autocmd StdinReadPre * let s:std_in=1
  autocmd VimEnter * if argc() == 0 && !exists("s:std_in") | NERDTree | endif

  " close vim if the only window left open is NERDTree
  autocmd bufenter * if (winnr("$") == 1 && exists("b:NERDTree") && b:NERDTree.isTabTree()) | q | endif

  " jump to last cursor position
  autocmd BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$") | exe "normal! g`\"" | endif
  autocmd BufReadPost COMMIT_EDITMSG,PULLREQ_EDITMSG exe "normal! gg"

  " assign file types
  autocmd BufNewFile,BufRead *.slide set filetype=present
  autocmd BufNewFile,BufRead *.template set filetype=yaml

  " language-dependent indenting
  autocmd FileType text,markdown,present setlocal ts=4 sw=4 et si tw=80
  autocmd FileType sh,perl,awk,python,dockerfile setlocal ts=4 sw=4 et si
  autocmd FileType ruby,cucumber,yaml,vim,gitcommit setlocal ts=2 sw=2 et si
  autocmd FileType c,h,cpp setlocal ts=8 sw=8 noet cindent
  autocmd FileType make setlocal ts=8 sw=8 noet
  autocmd FileType asm setlocal ts=8 sw=8 noet

  " auto-complete file names when committing
  autocmd FileType gitcommit setlocal iskeyword+=.

  " speed up Ruby syntax highlighting
  autocmd FileType ruby setlocal re=1 nocursorline

  " auto-complete Ruby names up to ! and ?
  autocmd FileType ruby setlocal iskeyword+=!,?

  " Go mappings
  autocmd FileType go nnoremap <leader>t :silent w\|:GoTest<cr>
  autocmd FileType go nnoremap <leader>T :silent w\|:GoTestFunc<cr>
  autocmd FileType go nnoremap <leader>a :silent w\|:!go test -v -cover ./...<cr>
  autocmd FileType go nnoremap <leader>r :silent w\|:GoRun<cr>
  autocmd FileType go nnoremap <leader>b :silent w\|:GoBuild<cr>

  " Rust mappings
  autocmd FileType rust nnoremap <leader>t :silent w\|:!cargo test -- %:t:r<cr>
  autocmd FileType rust nnoremap <leader>a :silent w\|:!cargo test<cr>
  autocmd FileType rust nnoremap <leader>r :silent w\|:!cargo run<cr>
  autocmd FileType rust nnoremap <leader>b :silent w\|:!cargo build<cr>

  " Terraform
  autocmd FileType terraform setlocal commentstring=#%s

  " enable spell-checking
  autocmd FileType text,markdown,gitcommit,present setlocal spell spelllang=en_us
augroup END

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" ADD LOCAL SETTINGS
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

if filereadable(expand("$HOME/.vimrc.local"))
  source $HOME/.vimrc.local
endif
